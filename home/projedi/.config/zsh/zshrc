###### General zsh configuration ######
ZSH=~/.config/zsh
ZSH_PLUGIN_DIR=${ZSH}/plugins

fpath=($ZSH_PLUGIN_DIR/zsh-completions/src $fpath)

autoload -U compinit promptinit colors
colors
compinit -d ${TMPPREFIX}_compdump
promptinit

zstyle ':completion:*' menu select
# nocompletealiases means treating aliases as, well, aliases
# while completealiases means treating them as new commands with their own completions
setopt nocompletealiases
setopt noclobber
setopt ignoreeof
setopt autocd
setopt correct
setopt INC_APPEND_HISTORY

PROMPT="%{$fg[cyan]%}[%~% ]%{$reset_color%}$ "
RPROMPT="%(?..[%{$fg_no_bold[red]%}%?%{$reset_color%}])"

HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.cache/zsh_history

###### Plugins. Who needs oh-my-zsh anyways? ######

source /usr/share/doc/pkgfile/command-not-found.zsh
source $ZSH_PLUGIN_DIR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
source $ZSH_PLUGIN_DIR/zsh-history-substring-search/zsh-history-substring-search.zsh
source $ZSH_PLUGIN_DIR/zsh-vim-mode/zsh-vim-mode.plugin.zsh
source $ZSH_PLUGIN_DIR/web-search/web-search.plugin.zsh
source $ZSH_PLUGIN_DIR/extract/extract.plugin.zsh
source $ZSH_PLUGIN_DIR/systemd/systemd.plugin.zsh

###### Aliases ######

alias pls='sudo'
alias poweroff='systemctl poweroff'
alias reboot='systemctl reboot'
alias mnt='udisksctl mount -b'
alias umnt='udisksctl unmount -b'
alias ls='ls --color=always'
alias l='ls -lah'
alias torr='transmission-remote'
alias open='xdg-open'
alias mpc="mpc -f '[[%artist% - ][%album% - ]%title%]'"
# Show progress while file copying
alias cpv='rsync -poghb --backup-dir=/tmp/rsync -e /dev/null --progress --'
alias cprv='rsync -r -poghb --backup-dir=/tmp/rsync -e /dev/null --progress --'
alias cdhmw='cd ~/Repos/Homework/Spring2013'
alias tags='cscope -Rb'
alias vim='vim -i ~projedi/.cache/viminfo -u ~projedi/.config/vim/vimrc'
alias gvim='gvim -i ~projedi/.cache/viminfo -u ~projedi/.config/vim/vimrc'
alias xsel='xsel -l ~/.cache/xsel.log'
alias gdb='gdb -x .config/gdbinit'
# vim here is alias
alias vimdiff='vim -d'
alias gg='git grep'
alias bd='. bd -s'

# TODO: make it also autocomplete.
function hoogle() {
   if [[ $1 == "data" || $1 == "--help" || $1 == "-?" ]]; then
      /home/projedi/.cabal/bin/hoogle $1
   else
      /home/projedi/.cabal/bin/hoogle $1 | head -n 10
   fi
}

###### Notify about comand completion ######

[[ $NOTIFY_COMMAND_COMPLETE_TIMEOUT == "" ]] && NOTIFY_COMMAND_COMPLETE_TIMEOUT=30

local last_command=""
local last_command_name=""

# Notify about the last command's success or failure.
function notify-command-complete() {
  local last_status=$?
  local -a ignore_commands
  ignore_commands=( vim man less )
  if [[ ${ignore_commands[(i)$last_command]} -le ${#ignore_commands} ]]; then
      true
  elif [[ -n $start_time ]]; then
     now=`date "+%s"`
     ((diff = $now - $start_time ))
     if (( $diff > $NOTIFY_COMMAND_COMPLETE_TIMEOUT )); then
        if [[ $last_status -gt "0" ]]; then
           notify-send "Job $last_command_name failed" "Exit code: $last_status"
        else
           notify-send "Job $last_command_name finished successfully"
        fi
     fi
  fi
  unset last_command start_time last_status
}

function store-command-stats() {
  last_command=$1
  #TODO: fix it
  #last_command_name=${1[(wr)^(*=*|sudo|ssh|-*)]}
  last_command_name=$last_command
  start_time=`date "+%s"`
}

###### From last working dir plugin ######
local cache_file="${TMPPREFIX}_last_working_dir"

function save-last-working-dir() {
	pwd >| "$cache_file"
}

[[ -r "$cache_file" ]] && cd "`cat $cache_file`"

###### Setting up hooks #######

preexec_functions=( store-command-stats )
precmd_functions=( notify-command-complete )
chpwd_functions=( save-last-working-dir )
